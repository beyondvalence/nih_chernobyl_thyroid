! Model building for small neoplastic nodules

new @

names 
IDN	
byear AGE_EXM	AGE_EXP	YEAR_EXM 
SEX_L	
OBL_M	OBL_86	UR_RUR	ur_rur86
thnod_prev	max_size nod_sz nod_sz3 thcount nod_neohc nod_neo
tc	prevcases FA_PREV 
DOSE_GYAV_S		
IODINE	tvolume TSH	TG	atg 
SMOKE_F	VITAM_F		
AIT_TOTF goiter_df_pal NDG_RELF ANY_FHF1
Cytology CytologyRecomend Operat OperatRecomend RefOnko fnb
@

skip 1 @

input 'E:\NCI\IR_Nod_Prev_BelAm\Data\dose_tnprev_belam_exclude1.csv' @

tran dgy=dose_gyav_s;
     dgysq=dgy*dgy;
     ex8=(age_exp-8);
     ex8sq=ex8*ex8;
     age21=(age_exm-21);
     age21sq=age21*age21;
     tvol13=(tvolume-13);
     tvol13sq=tvol13*tvol13;
     yob1983=(byear-1983);

     if any_fhf1==-1 then any_fhf1=2 !2 recoded to missing
     endif; 

     if smoke_f==-1 then smoke_f=2
     endif;

     
     if ismiss(goiter_df_pal)==1 then goiter_df_pal=2
     endif;


     if obl_m==1 then obl_mn=2 !Gomel
     elif obl_m==2 or obl_m==4 then obl_mn=1 !Minsk
     else obl_mn=3 !Mogilev and others
     endif;

     if obl_86==1 then obl_86n=2 !Gomel
     elif obl_86==2 or obl_86==4 then obl_86n=1 !Minsk
     else obl_86n=3 !Mogilev and others
     endif;

     if ur_rur86==1 then ur_rur86n=1 !Rural
     elif ur_rur86==2 or ur_rur86==3 or ur_rur86==4 then ur_rur86n=2 !Urban
     endif;

     if tvolume>=18 and sex_l==1 then tvlarge=1 !Thyroid enlargement for women
     elif tvolume>=25 and sex_l==0 then tvlarge=1  !Thyroid enlargement for men
     else tvlarge=0 !Normal thyroid
     endif;

     iod100=(iodine-100);
     iod60=(iodine-60);
     iodinen=iodine;
     if ismiss(iodine)==1 then iodinen=9999
     endif; 


@
 
levels   
SEX_L	 
OBL_M	obl_mn OBL_86 obl_86n	UR_RUR	ur_rur86 ur_rur86n
thnod_prev nod_sz nod_sz3 thcount nod_neohc nod_neo
tc	prevcases FA_PREV 
SMOKE_F	VITAM_F		
AIT_TOTF goiter_df_pal NDG_RELF ANY_FHF1
Cytology CytologyRecomend Operat OperatRecomend RefOnko fnb
tvlarge 
@

categ byear as dobyrct </1973/ 1978/ 1983/ 1987/ > @
categ age_exm as agect </ 18/ 25/ > @
categ age_exp as exct </ 5/ 12/ > @
categ year_exm as yearexmct </ 1998/ 1999/ 2000/ 2001/> @
categ tvolume as tvct </ 8/ 10/ 14/ > @
categ iodinen as iodct </30/ 50/ 70/9998> @
categ dgy as dct7  </ 0.1000/ 0.2500/ 0.500/ 1.000/ 2.000/ 4.000/> @

tran 
if        dct7==1 then dct7num=1
     elif dct7==2 then dct7num=2
     elif dct7==3 then dct7num=3
     elif dct7==4 then dct7num=4
     elif dct7==5 then dct7num=5
     elif dct7==6 then dct7num=6
     else              dct7num=7
     endif;

@

! Table 3 Cases
cases thnod_prev @

freq nod_neo  thnod_prev @
freq nod_neo  nod_sz @

select ((nod_sz==1 and nod_neo==1) or thnod_prev==0)@

freq nod_neo  nod_sz @

! Background
! A priori: sex, attained age, age at exposure 
! To examine: UR_RUR, oblast, iodine,  tvlarge, goiter_df_pal,any_fhf1, vitam_f

!Urban rural
nomodel @
logl 0 sex_l age21  ex8 @
line 1 dgy@
fit@ null@
logl 0 +UR_RUR  @ fit@ lrt@
logl 0 -UR_RUR +ur_rur86n  @ fit@ lrt@

!Oblast
nomodel @
logl 0 sex_l age21  ex8 UR_RUR@
line 1 dgy@
fit@ null@
logl 0 +obl_mn  @ fit@ lrt@
logl 0 -obl_mn +obl_86n  @ fit@ lrt@


!Iodine
select ((nod_sz==1 and nod_neo==1) or thnod_prev==0) and ismiss(iodine)==0@
nomodel @
logl 0 sex_l age21  ex8 ur_rur @
line 1 dgy@
fit@ null@
logl 0 +iodine  @ fit@ lrt@
select((nod_sz==1 and nod_neo==1) or thnod_prev==0)@


!Thyroid volume enlargement
nomodel @
logl 0 sex_l age21  ex8 ur_rur @
line 1 dgy@
fit@ null@
logl 0 +tvlarge @ fit@ lrt@


!Presence of diffuse goiter as diagnosed at palpation (at 1st screening)
nomodel @
logl 0 sex_l age21  ex8 ur_rur  tvlarge@
line 1 dgy@
fit@ null@
logl 0 +goiter_df_pal @ fit@ lrt@


!Family history of any thyroid disease
nomodel @
logl 0 sex_l age21  ex8 ur_rur tvlarge goiter_df_pal@
line 1 dgy@
fit@ null@
logl 0 +any_fhf1 @ fit@ lrt@


!Oblast again
nomodel @
logl 0 sex_l age21  ex8 ur_rur tvlarge goiter_df_pal any_fhf1@
line 1 dgy@
fit@ null@
logl 0 +obl_mn  @ fit@ lrt@
logl 0 -obl_mn +obl_86n  @ fit@ lrt@

!Final background:
nomodel@
logl 0 sex_l age21  ex8 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
fit@

!Table 2 categorical dose response ORs
nomodel@
logl 0 sex_l age21  ex8 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 dct7@
fit@
profile 18@ profile 19@ profile 20@ profile 21@ profile 22@ profile 23@ 


!Table 2 categorical dose response p-value

nomodel@
logl 0 sex_l age21  ex8 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
fit@ null@
logl 0 +dct7num@ fit@ lrt@

!Figure 1 shape of dose response

!linear dose model AIC=373*BEST
nomodel@
logl 0 sex_l age21  ex8 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
fit@ null@

!linear quadratic dose-response AIC=374
line 1 +dgysq @ 
fit @ lrt @

!linear exponential dose-response AIC=374 
line 1 -dgysq@
logl 1 dgy @
fit@  lrt@


!Figure 3 dose response <4 Gy
select ((nod_sz==1 and nod_neo==1) or thnod_prev==0) and dgy<4@

!Confirm linear dose-response <4 Gy
!Basic linear dose model 
nomodel@
logl 0 sex_l age21  ex8 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
fit@ null@

!linear quadratic dose-response 
line 1 +dgysq @ 
fit @ lrt @

!linear exponential dose-response
line 1 -dgysq@
logl 1 dgy @
fit@  lrt@

!Final model <4 Gy
nomodel@
logl 0 sex_l age21  ex8 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
fit@
profile 19@

