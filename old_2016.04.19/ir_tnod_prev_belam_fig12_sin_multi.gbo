! Figure 1 and 2
! Model building for single and multiple nodules, whole dose range

new @

names 
IDN	
byear AGE_EXM	AGE_EXP	YEAR_EXM 
SEX_L	
OBL_M	OBL_86	UR_RUR	ur_rur86
thnod_prev	max_size nod_sz nod_sz3 thcount nod_neohc nod_neo
tc	prevcases FA_PREV 
DOSE_GYAV_S		
IODINE	tvolume TSH	TG	atg 
SMOKE_F	VITAM_F		
AIT_TOTF goiter_df_pal NDG_RELF ANY_FHF1
Cytology CytologyRecomend Operat OperatRecomend RefOnko fnb
@

skip 1 @

input 'C:\REB\chernobyl_thyroid\Data\dose_tnprev_belam_exclude1.csv' @

tran dgy=dose_gyav_s;
     dgysq=dgy*dgy;
     ex2=(age_exp-2);
     ex4=(age_exp-4);
     ex5=(age_exp-5);
     ex8=(age_exp-8);
     ex8sq=ex8*ex8;
     age21=(age_exm-21);
     age21sq=age21*age21;
     lage21 = log(age_exm/21) ;
     lage21sq = lage21^2 ;
     tvol13=(tvolume-13);
     tvol13sq=tvol13*tvol13;
     yob1983=(byear-1983);

     if any_fhf1==-1 then any_fhf1=2 !2 recoded to missing
     endif; 

     if smoke_f==-1 then smoke_f=2
     endif;
     
     if ismiss(goiter_df_pal)==1 then goiter_df_pal=2
     endif;


     if obl_m==1 then obl_mn=2 !Gomel
     elif obl_m==2 or obl_m==4 then obl_mn=1 !Minsk
     else obl_mn=3 !Mogilev and others
     endif;

     if obl_86==1 then obl_86n=2 !Gomel
     elif obl_86==2 or obl_86==4 then obl_86n=1 !Minsk
     else obl_86n=3 !Mogilev and others
     endif;

     if ur_rur86==1 then ur_rur86n=1 !Rural
     elif ur_rur86==2 or ur_rur86==3 or ur_rur86==4 then ur_rur86n=2 !Urban
     endif;

     if tvolume>=18 and sex_l==1 then tvlarge=1 !Thyroid enlargement for women
     elif tvolume>=25 and sex_l==0 then tvlarge=1  !Thyroid enlargement for men
     else tvlarge=0 !Normal thyroid
     endif;

     iod100=(iodine-100);
     iod60=(iodine-60);
     iodinen=iodine;
     if ismiss(iodine)==1 then iodinen=9999
     endif; 


@
 
levels   
SEX_L	 
OBL_M	obl_mn OBL_86 obl_86n	UR_RUR	ur_rur86 ur_rur86n
thnod_prev nod_sz nod_sz3 thcount nod_neohc nod_neo
tc	prevcases FA_PREV 
SMOKE_F	VITAM_F		
AIT_TOTF goiter_df_pal NDG_RELF ANY_FHF1
Cytology CytologyRecomend Operat OperatRecomend RefOnko fnb
tvlarge 
@

categ byear as dobyrct </1973/ 1978/ 1983/ 1987/ > @
categ age_exm as agect </ 18/ 25/ > @
categ age_exp as exct </2/5/ > @ 
categ age_exp as exct4 </1/2/5 > @ 
categ year_exm as yearexmct </ 1998/ 1999/ 2000/ 2001/> @
categ tvolume as tvct </ 8/ 10/ 14/ > @
categ iodinen as iodct </20/9998> @
categ dgy as dct7  </ 0.1000/ 0.2500/ 0.500/ 1.000/ 2.000/ 4.000/> @
categ dgy as dct8  </ 0.1000/ 0.2500/ 0.500/ 1.000/ 2.000/ 4.000/ 6.000/ > @

@

! Single

select (thcount==1 or thnod_prev==0)@

cases thnod_prev @

freq  thnod_prev @

! Background
! A priori: sex,log  attained age, age at exposure , obl_86n , UR_RUR
! To examine: iodine,  tvlarge, goiter_df_pal,any_fhf1, vitam_f


!Iodine
select (thcount==1 or thnod_prev==0) and ismiss(iodine)==0@
nomodel @
logl 0 sex_l lage21  ex5 ur_rur obl_86n @
line 1 dgy@
fit@ null@
logl 0 +iodine  @ fit@ lrt@
select (thcount==1 or thnod_prev==0)@ 

!Thyroid volume enlargement
nomodel @
logl 0 sex_l lage21  ex5 ur_rur obl_86n@
line 1 dgy@
fit@ null@
logl 0 +tvlarge @ fit@ lrt@ 


!Presence of diffuse goiter as diagnosed at palpation (at 1st screening)
nomodel @
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge@
line 1 dgy@
fit@ null@
logl 0 +goiter_df_pal @ fit@ lrt@ 


!Family history of any thyroid disease
nomodel @
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal@
line 1 dgy@
fit@ null@
logl 0 +any_fhf1 @ fit@ lrt@ 



!Final background: sex_l lage21  ex5 ur_rur obl_86n  tvlarge goiter_df_pal any_fhf1


!Interaction by ex5 and shape of dose response

!Basic linear dose model **** Best model

nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
logl 1 ex5 @
fit@ null@

!sex interaction 
logl 1 +sex_l @
fit@ lrt@
logl 1 -sex_l @
fit@null@
!linear exponential dose-response 

logl 1 +dgy@ 
fit@ lrt@ 

!linear quadratic dose-response 

logl 1 - dgy @
line 1 +dgysq@
fit@  lrt@


! Final model for Figures 1, 2 single

nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
logl 1 ex5@
fit@ 
bounds 19@ !Figure 1 Dose reasponse plot at age 5
bounds 20@ !Figure 2 Age at exposure plot

!Figure 1 points (bounds not needed) but add 1 to estimates

nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dct7@
logl 1 ex5@
para 19=0@
fit@ 

!Figure 1 points (bounds not needed) but add 1 to estimates
! test dct8
nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dct8@
logl 1 ex5@
para 19=0@
fit@ 

!Figure 1 points (bounds not needed) but add 1 to estimates
! test dcat8

nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dct8@
logl 1 ex5@
para 19=0@
fit@ 

! Points and CIs for Figure 2 single 

nomodel@
logl 0 sex_l lage21  exct4 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
logl 1 exct4@
para 22=1@
fit@ 
bounds 23@ 
bounds 24@ 
bounds 25@ 
bounds 26@



!Multiple

select (thcount>1 or thnod_prev==0)@

cases thnod_prev @

freq  thnod_prev @

! Background
! A priori: sex_l lage21  ex5 ur_rur obl_86n  
! To examine: iodine,  tvlarge, goiter_df_pal,any_fhf1, vitam_f


!Iodine
select (thcount>1 or thnod_prev==0) and ismiss(iodine)==0@
nomodel @
logl 0 sex_l lage21  ex5 ur_rur obl_86n   @
line 1 dgy@
fit@ null@
logl 0 +iodine  @ fit@ lrt@
select (thcount>1 or thnod_prev==0)@ 

!Thyroid volume enlargement
nomodel @
logl 0 sex_l lage21  ex5 ur_rur obl_86n @
line 1 dgy@
fit@ null@
logl 0 +tvlarge @ fit@ lrt@ 


!Presence of diffuse goiter as diagnosed at palpation (at 1st screening)
nomodel @
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge @
line 1 dgy@
fit@ null@
logl 0 +goiter_df_pal @ fit@ lrt@ 


!Family history of any thyroid disease
nomodel @
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal@
line 1 dgy@
fit@ null@
logl 0 +any_fhf1 @ fit@ lrt@ !Does not change risk estimate


!Final background: sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1


!Interaction by ex5 and shape of dose response

!Basic linear dose model 

nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
logl 1 ex5 @
fit@ null@

logl 1 +sex_l@
fit@ lrt@
logl 1-sex_l@
fit@ null@

!linear exponential dose-response **** Best model

logl 1 +dgy@ 
fit@ lrt@ 

!linear quadratic dose-response 

logl 1 - dgy @
line 1 +dgysq@
fit@  lrt@


! Final model for Figures 1, 2 multiple

nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
logl 1 dgy ex5@
fit@ 
bounds 19@ !Figure 1 Dose reasponse plot at age 5
bounds 20@
bounds 21@ !Figure 2 Age at exposure plot


!Figure 1 points (bounds not needed) but add 1 to estimates

nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dct7@
logl 1 ex5@
para 19=0@
fit@ 

!Figure 1 points (bounds not needed) but add 1 to estimates
! test dcat8

nomodel@
logl 0 sex_l lage21  ex5 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dct8@
logl 1 ex5@
para 19=0@
fit@ 

mean dgy by dct8 @

! Points and CIs for Figure 2 multiple 

nomodel@
logl 0 sex_l lage21  exct4 ur_rur obl_86n tvlarge goiter_df_pal any_fhf1 @
line 1 dgy@
logl 1 exct4@
para 22=1@
fit@ 
bounds 23@ 
bounds 24@ 
bounds 25@ 
bounds 26@



